namespace modifies Array {
	function each( array a ) {
		if( recipient() != null ) {
			number i = 0;
			number size = a.size();
			for( i = 0; i < size; i++ ) {
				void rval = deliver(a[i]);
				if( rval == false or rval == null )
					return false;
			}
		}
		return true;
	}

	function join( array a, string value ) {
		number size = a.size();
		number last = size - 1;
		string result;
		for( number i = 0; i < size; i++ ) {
			if( recipient() != null ) {
				result += deliver(a[i]);
			} else {
				result += a[i];
			}
			if( i < last ) {
				result += value;
			}
		}
		return result;
	}

	function append( array a, array b ) {
		b.each() using ( value ) {
			a[] = value;
		};
		return a;
	}

	function sort( array list ) {
		array left;
		array right;
		number size;
		number pivot;

		if( not recipient() )
			return list;

		if( list.size() < 2 )
			return list;

		pivot = deliver(list[0]);
		list.del(0);

		size = list.size();
		for( number i = 0; i < size; i++ ) {
			if( deliver(list[i]) < pivot )
				left[] = list[i];
			else
				right[] = list[i];
		}

		left = left.sort() using recipient();
		right = right.sort() using recipient();

		return left.append([ pivot ].append(right));
	}
}